
%import common.DIGIT
%import .dates.DAY -> IMM_INDEX
%import .dates (MONTH_CODE, QUARTER_END_MONTH_CODE, QUARTERLY_IMM_DATE)
%import .numbers (INT, QUARTERED_FLOAT)

// **********************************
// STANDARD
// **********************************

DAY_UNIT: "D"
STRICT_DAY_TENOR: "0".."7" DAY_UNIT

BUSINESS_DAY_UNIT: "B"
STRICT_BUSINESS_DAY_TENOR: "0".."5" BUSINESS_DAY_UNIT

WEEK_UNIT: "W"
STRICT_WEEK_TENOR: "1".."4" WEEK_UNIT

MONTH_UNIT: "M"
MONTH_INT: INT
STRICT_MONTH_INT: ("1".."9" | "1" ("0".."2" | "5" | "8") | "2" ("1".."4" | "7") | "30" | "42" | "54")
STRICT_MONTH_TENOR: STRICT_MONTH_INT MONTH_UNIT

YEAR_UNIT: "Y"
YEAR_INT: INT
YEAR_TENOR: YEAR_INT YEAR_UNIT
FLOAT_YEAR_TENOR: QUARTERED_FLOAT YEAR_UNIT

TENOR_UNIT: DAY_UNIT | BUSINESS_DAY_UNIT | WEEK_UNIT | MONTH_UNIT | YEAR_UNIT
TENOR: INT TENOR_UNIT
FLOAT_TENOR: QUARTERED_FLOAT TENOR_UNIT
STRICT_TENOR: STRICT_DAY_TENOR | STRICT_BUSINESS_DAY_TENOR | STRICT_WEEK_TENOR | STRICT_MONTH_TENOR | FLOAT_YEAR_TENOR


// **********************************
// IMM
// **********************************

IMM_TENOR: MONTH_CODE DIGIT
QUARTERLY_IMM_TENOR: QUARTER_END_MONTH_CODE DIGIT

RELATIVE_QUARTERLY_IMM: "IMM_" IMM_INDEX
ABSOLUTE_QUARTERLY_IMM: "IMM_" QUARTERLY_IMM_DATE


